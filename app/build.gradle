apply plugin: 'com.android.application'

for (String taskName : getGradle().getStartParameter().getTaskNames()) {
    if (taskName.endsWith('RcxDebug') || taskName.endsWith('RcxRelease')) {
        apply plugin: 'com.google.gms.google-services'
        apply plugin: 'com.google.firebase.crashlytics'
        break
    }
}

tasks.whenTaskAdded { task ->
    // We defer the build of rclone just before the assemble stage of the
    // main app, so that Android Studio displays a convenient clickable
    // error message that allows to install the proper NDK version
    // directly from the SDK manager.
    if (task.name.startsWith('assemble')) {
        task.dependsOn(':rclone:buildNative')
    }
}

android {
    buildToolsVersion project.properties['io.github.x0b.rcx.buildToolsVersion']
    ndkVersion project.properties['io.github.x0b.rcx.ndkVersion']

    signingConfigs {
        github_x0b {
            keyAlias 'github_x0b'
        }
    }
    compileSdkVersion 29
    defaultConfig {
        applicationId 'io.github.x0b.rcx'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 170 // last digit is reserved for ABI, only ever end on 0!
        versionName '1.11.4'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def documentsAuthority = applicationId + '.documents'
        manifestPlaceholders = [documentsAuthority: documentsAuthority]
        buildConfigField "String", "DOCUMENTS_AUTHORITY", "\"${documentsAuthority}\""
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            buildConfigField "java.lang.long", "BUILD_TIME", "0L"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "java.lang.long", "BUILD_TIME", String.valueOf(System.currentTimeMillis()) + "L"
        }
    }

    sourceSets {
        all {
            jniLibs.srcDirs = ["lib"]
        }
    }

    flavorDimensions 'edition'
    productFlavors {
        // Default GitHub and Google Play build
        rcx {
            dimension 'edition'
        }
        // OSS build
        oss {
            dimension 'edition'
            buildConfigField "java.lang.long", "BUILD_TIME", "0L"
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk true
        }
    }

    project.ext.versionCodes = [
        'armeabi-v7a': 6,
        'arm64-v8a': 7,
        'x86': 8,
        'x86_64': 9
    ]

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) + android.defaultConfig.versionCode
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenCentral()
    google()
    jcenter()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.core:core:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.leinardi.android:speed-dial:2.0.0'
    implementation 'org.markdownj:markdownj-core:0.4'
    implementation 'jp.wasabeef:recyclerview-animators:2.3.0'
    implementation 'com.github.GrenderG:Toasty:1.3.0'
    // Firebase & Crashlytics
    rcxImplementation 'com.google.firebase:firebase-analytics:17.3.0'
    rcxImplementation 'com.google.firebase:firebase-crashlytics:17.0.0-beta03'
    // Thumbnails
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    // REST Client
    implementation 'com.squareup.okhttp3:okhttp:4.2.0'
    debugImplementation 'com.squareup.okhttp3:logging-interceptor:4.2.0'
    // JSON
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.9'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.9'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.9'
    // Timestmps
    implementation 'com.github.x0b:rfc3339parser:2.0.0'
    // SAF/WebDAV
    implementation project(':safdav')
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    // Java 8+ library support
    // TODO: replace with coreLibraryDesugar once available
    implementation 'net.sourceforge.streamsupport:android-retrostreams:1.7.1'
    implementation 'net.sourceforge.streamsupport:android-retrofuture:1.7.1'
    // TEST
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha02'
}
